///////////////////////////////////////////////////////////////////////////////
//
// Служебный модуль с реализацией работы команды <exec-rules>
//
// (с) BIA Technologies, LLC
//
///////////////////////////////////////////////////////////////////////////////

#Использовать gitrunner

Перем Лог;
Перем РепозиторийGit;

///////////////////////////////////////////////////////////////////////////////

Процедура НастроитьКоманду(Знач Команда, Знач Парсер) Экспорт
	
	// Добавление параметров команды
	Парсер.ДобавитьПозиционныйПараметрКоманды(Команда, "КаталогРепозитория",
		"Каталог анализируемого репозитория");
	Парсер.ДобавитьИменованныйПараметрКоманды(Команда, "-source-dir",
		"Каталог расположения исходных файлов относительно корня репозитория. 
		| Если сценариев несколько,  указываются в кавычках через , . По умолчанию <src>");
	Парсер.ДобавитьИменованныйПараметрКоманды(Команда, "-rules",
		"Перечень правил для применения. Если сценариев несколько,  указываются в кавычках через ,");
	Парсер.ДобавитьИменованныйПараметрКоманды(Команда, "-cfg-file",
		"Путь к конфигурационному файлу с настройками. Если не указан, используются настройки репозитория либо глобальные");
	
КонецПроцедуры // НастроитьКоманду

// Выполняет логику команды
//
// Параметры:
//   ПараметрыКоманды - Соответствие - Соответствие ключей командной строки и их значений
//   Приложение - Модуль - Модуль менеджера приложения
//
Функция ВыполнитьКоманду(Знач ПараметрыКоманды, Знач Приложение) Экспорт
	
	Лог = Приложение.ПолучитьЛог();
	
	КаталогРепозитория = ПараметрыКоманды["КаталогРепозитория"];
	ФайлКаталогРепозитория = Новый Файл(КаталогРепозитория);
	Если НЕ ФайлКаталогРепозитория.Существует() ИЛИ ФайлКаталогРепозитория.ЭтоФайл() Тогда
		
		Лог.Ошибка("Каталог репозитория '%1' не существует или это файл", КаталогРепозитория);
		Возврат Приложение.РезультатыКоманд().НеверныеПараметры;
		
	КонецЕсли;
	
	АдресПоискаКонфигурационногоФайла = КаталогРепозитория;
	
	Если ЗначениеЗаполнено(ПараметрыКоманды["-cfg-file"]) Тогда
		
		АдресПоискаКонфигурационногоФайла = ПараметрыКоманды["-cfg-file"];
		
	КонецЕсли;
	
	КаталогИсходныхФайлов = ПараметрыКоманды["-source-dir"];
	Если Не ЗначениеЗаполнено(КаталогИсходныхФайлов) Тогда
		
		КаталогИсходныхФайлов = "src";
	
	Иначе

		КаталогиИсходныхФайлов = СтрЗаменить(ПараметрыКоманды["-source-dir"], " ", "");
		МассивКаталоговИсходныхФайлов = СтрРазделить(КаталогиИсходныхФайлов, ",", Ложь);

	КонецЕсли;
	
	ОбрабатываемыеФайлы = Новый Массив();

	Для Каждого КаталогИсходныхФайлов Из МассивКаталоговИсходныхФайлов Цикл
	
		ТекущийКаталогИсходныхФайлов = ОбъединитьПути(КаталогРепозитория, КаталогИсходныхФайлов);
		ФайлТекущийКаталогИсходныхФайлов = Новый Файл(ТекущийКаталогИсходныхФайлов);
		ТекущийКаталогИсходныхФайлов = ФайлТекущийКаталогИсходныхФайлов.ПолноеИмя;
		Если НЕ ФайлТекущийКаталогИсходныхФайлов.Существует() Тогда

			Лог.Ошибка("Каталога <%1> не существует", ТекущийКаталогИсходныхФайлов);
		
		КонецЕсли;

		КаталогРепозитория = ФайлКаталогРепозитория.ПолноеИмя;
		
		ОбрабатываемыеФайлыКаталога = НайтиФайлы(ТекущийКаталогИсходныхФайлов, "*", Истина);

		Для Каждого ОбрабатываемыйФайлКаталога Из ОбрабатываемыеФайлыКаталога Цикл
			
			ОбрабатываемыеФайлы.Добавить(ОбрабатываемыйФайлКаталога);

		КонецЦикла;

	КонецЦикла;

	ИменаЗагружаемыхСценариев = Неопределено;
	Если ЗначениеЗаполнено(ПараметрыКоманды["-rules"]) Тогда
		ПараметрИменаСценариев = СтрЗаменить(ПараметрыКоманды["-rules"], " ", "");
		ИменаЗагружаемыхСценариев = СтрРазделить(ПараметрИменаСценариев, ",", Ложь);
	КонецЕсли;
		
	Ит = 0;
	
	УправлениеНастройками = МенеджерНастроек.НастройкиРепозитория(АдресПоискаКонфигурационногоФайла);
	НаборНастроек = СценарииОбработки.ПолучитьСценарииСПараметрамиВыполнения(КаталогРепозитория, ИменаЗагружаемыхСценариев);
	КритичныеОшибки = Новый Массив;
	
	ПараметрыОбработки = СценарииОбработки.ПолучитьСтандартныеПараметрыОбработки();

	Пока Ит < ОбрабатываемыеФайлы.Количество() Цикл
		
		АнализируемыйФайл = ОбрабатываемыеФайлы[Ит];
		
		Если АнализируемыйФайл.ЭтоКаталог() Тогда
			Ит = Ит + 1;
			Продолжить;
		КонецЕсли;
		
		Лог.Информация("Анализируется файл <%1>", АнализируемыйФайл.ПолноеИмя);
		
		ИмяФайла = ФайловыеОперации.ПолучитьНормализованныйОтносительныйПуть(КаталогРепозитория,
				СтрЗаменить(АнализируемыйФайл.ПолноеИмя, КаталогРепозитория, ""));
		
		ИмяПроекта = МенеджерНастроек.ИмяПроектаДляФайла(ИмяФайла);
		
		НастройкаОбработки = НаборНастроек[ИмяПроекта];
		
		Если НЕ ЗначениеЗаполнено(НастройкаОбработки) Тогда
			
			ВызватьИсключение СтрШаблон("Не удалось получить настройки для %1", ИмяФайла);
			
		КонецЕсли;
		
		ПараметрыОбработки.Настройки = НастройкаОбработки.НастройкиСценариев.Получить("НастройкиСценариев");
		ПараметрыОбработки.КаталогРепозитория = КаталогРепозитория;
		
		Для Каждого СценарийОбработки Из НастройкаОбработки.СценарииОбработки Цикл
			
			Попытка
				ФайлОбработан = СценарийОбработки.ОбработатьФайл(АнализируемыйФайл,
						ТекущийКаталогИсходныхФайлов,
						ПараметрыОбработки);
				
				Если НЕ ФайлОбработан Тогда
					Продолжить;
				КонецЕсли;
				
				Для Каждого ФайлДляДопОбработки Из ПараметрыОбработки.ФайлыДляПостОбработки Цикл
					
					ОбрабатываемыеФайлы.Добавить(ФайловыеОперации.НовыйФайл(ФайлДляДопОбработки));
					
				КонецЦикла;
				
				ПараметрыОбработки.ФайлыДляПостОбработки.Очистить();
				
			Исключение
				
				Ошибка = ОписаниеОшибки();
				КритичныеОшибки.Добавить(Ошибка);
				Лог.Информация("Критичная ошибка: %1", Ошибка);
				
			КонецПопытки;
			
		КонецЦикла;
		
		Ит = Ит + 1;
		
	КонецЦикла;
	
	// В отличие от прекоммита, здесь лучше вывести все криты в конце одним блоком еще раз.
	Если КритичныеОшибки.Количество() Тогда
		
		Лог.Ошибка("В результате выполнения возникли исключения:");
		
		Для Каждого ОписаниеОшибки Из КритичныеОшибки Цикл
			
			Лог.Ошибка(ОписаниеОшибки);
			
		КонецЦикла;
		
	КонецЕсли;
	
	// При успешном выполнении возвращает код успеха
	Возврат Приложение.РезультатыКоманд().Успех;
	
КонецФункции // ВыполнитьКоманду

