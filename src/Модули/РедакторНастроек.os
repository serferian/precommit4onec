Процедура СброситьНастройкиРепозитория(СброситьНастройкиПодпроектов = Ложь, Знач Подпроект = "") Экспорт
	
	Если МенеджерНастроек.ЭтоНовый() Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Подпроект) Тогда
		
		МенеджерНастроек.УдалитьПроект(Подпроект);
		МенеджерНастроек.ЗаписатьНастройки();

	ИначеЕсли СброситьНастройкиПодпроектов Тогда 
		
		МенеджерНастроек.УдалитьПроекты();
		МенеджерНастроек.ЗаписатьНастройки();

	Иначе
		
		МенеджерНастроек.УдалитьНастройки(); // Удаляем ветку настроек прекоммит
		
	КонецЕсли;

КонецПроцедуры

Процедура СброситьГлобальныеНастройки() Экспорт

	Если МенеджерНастроек.ИспользуютсяЛокальныеНастройки() Тогда
	
		ВызватьИсключение 
		"Обнаружен не корректный вызов. 
		|Для сброса глобальных настроек необходимо загрузить глобальные настройки 
		|вызвав МенеджерНастроек.ГлобальныеНастройки";

	КонецЕсли;

	ИмяПриложения 	= МенеджерНастроек.КлючНастройкиPrecommit();
	Настройки 		= МенеджерНастроек.ГлобальныеНастройки();
	
	СтандартныеНастройки = ПолучитьСтандартнуюСтруктуруНастроек();
	
	ГлобальныеСценарии = СценарииОбработки.ГлобальныеСценарии();
	НастройкиСценариев = СценарииОбработки.НастройкиГлобальныхСценариев();
	
	СтандартныеНастройки.Вставить("ГлобальныеСценарии", ГлобальныеСценарии);
	СтандартныеНастройки.Вставить("НастройкиСценариев", НастройкиСценариев);
	
	Настройки.ЗаписатьНастройкиПриложения(ИмяПриложения, СтандартныеНастройки);
	
КонецПроцедуры

Функция ПолучитьСтандартнуюСтруктуруНастроек() Экспорт
	
	Настройки = Новый Соответствие();
	
	Настройки.Вставить("ИспользоватьСценарииРепозитория", Ложь);
	Настройки.Вставить("КаталогЛокальныхСценариев", "");
	Настройки.Вставить("ГлобальныеСценарии", Новый Массив);
	Настройки.Вставить("ОтключенныеСценарии", Новый Массив);
	Настройки.Вставить("НастройкиСценариев", Новый Соответствие());
	Возврат Настройки;
	
КонецФункции

Процедура ЗаписатьНастройкиРепозитория(НовыеНастройки) Экспорт
	
	УправлениеНастройками = МенеджерНастроек.Настройки();

	Для каждого НоваяНастройка Из НовыеНастройки Цикл
		УправлениеНастройками.ЗаписатьНастройкиПриложения(НоваяНастройка.Ключ, НоваяНастройка.Значение);
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбновитьНастройки(БазовыеНастройки, НастройкиПроектов) Экспорт

	Лог = МенеджерПриложения.ПолучитьЛог();

	Лог.Отладка("Обновление настроек");
	КлючПроекты = МенеджерНастроек.КлючПроекты();

	Если БазовыеНастройки = Неопределено Тогда

		БазовыеНастройки = МенеджерНастроек.НастройкиПроекта("");

	Иначе

		БазовыеНастройки.Вставить(КлючПроекты, Новый Соответствие());

		Для Каждого ИмяПроекта Из МенеджерНастроек.ПроектыКонфигурации() Цикл

			БазовыеНастройки[КлючПроекты].Вставить(ИмяПроекта, МенеджерНастроек.НастройкиПроекта(ИмяПроекта));
			
		КонецЦикла;

	КонецЕсли;
	
	Если ЗначениеЗаполнено(НастройкиПроектов) Тогда

		Если БазовыеНастройки[КлючПроекты] = Неопределено Тогда

			БазовыеНастройки.Вставить(КлючПроекты, Новый Соответствие());

		КонецЕсли;

		Для Каждого Элемент Из НастройкиПроектов Цикл

			БазовыеНастройки[КлючПроекты].Вставить(Элемент.Ключ, Элемент.Значение);
			
		КонецЦикла;
	
	КонецЕсли;

	МенеджерНастроек.Настройки().ЗаписатьНастройкиПриложения(МенеджерНастроек.КлючНастройкиPrecommit(), БазовыеНастройки);

КонецПроцедуры